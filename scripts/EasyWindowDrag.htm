<!DOCTYPE HTML>
<html>
<head>
<title>Einfache Fensterverschiebung (benötigt XP/2k/NT)</title>
<meta http-equiv="content-type" content="text/html; charset=UTF-8">
<meta http-equiv="X-UA-Compatible" content="IE=edge">
<meta name="robots" content="noindex, nofollow">
<link href="../static/theme.css" rel="stylesheet" type="text/css" />
<script src="../static/content.js" type="text/javascript"></script>
<script src="../static/ga.js" type="text/javascript"></script>
</head>
<body>

<h1>Einfache Fensterverschiebung (benötigt XP/2k/NT)</h1>

<p>Normalerweise kann ein Fenster nur verschoben werden, wenn die Titelleiste angeklickt wird. Dieses Skript erweitert diese Funktion, sodass jeder Punkt in einem Fenster angeklickt werden kann. Um diesen Modus zu aktivieren, drückt man die linke Maustaste, während man die <kbd>Feststell</kbd>-Taste oder mittlere Maustaste gedrückt hält, und zieht das Fenster auf eine neue Position.
</p>
<p><a href="EasyWindowDrag.ahk">Dieses Skript herunterladen</a>  |  <a href="index.htm">Andere Beispiel-Skripte</a>  |  <a href="../AutoHotkey.htm">Home</a></p>

<pre class="NoIndent"><em>; Hinweis: Die Feststelltaste oder mittlere Maustaste kann optional losgelassen werden, nachdem
; die Maustaste gedrückt wurde, anstatt die Taste die ganze Zeit gedrückt zu halten.</em>

~MButton &amp; LButton::
CapsLock &amp; LButton::
EWD_MoveWindow()
{
    CoordMode "Mouse"  <em>; Auf Bildschirm-/absolute Koordinaten wechseln.</em>
    MouseGetPos EWD_MouseStartX, EWD_MouseStartY, EWD_MouseWin
    WinGetPos EWD_OriginalPosX, EWD_OriginalPosY,,, "ahk_id " EWD_MouseWin
    if !WinGetMinMax("ahk_id " EWD_MouseWin)  <em>; Nur, wenn das Fenster nicht maximiert ist </em>
        SetTimer "EWD_WatchMouse", 10 <em>; Den Mauszeiger verfolgen, wenn er vom Benutzer gezogen wird.</em>

    EWD_WatchMouse()
    {
        if !GetKeyState("LButton", "P")  <em>; Taste wurde losgelassen, daher ist das Ziehen abgeschlossen.</em>
        {
            SetTimer , "Off"
            return
        }
        if GetKeyState("Escape", "P")  <em>; Escape wurde gedrückt, daher wird das Ziehen abgebrochen.</em>
        {
            SetTimer , "Off"
            WinMove EWD_OriginalPosX, EWD_OriginalPosY,,, "ahk_id " EWD_MouseWin
            return
        }
        <em>; Ansonsten das Fenster neu positionieren, damit es den Mauskoordinaten entspricht,
        ; bedingt durch das Ziehen des Mauszeigers:</em>
        CoordMode "Mouse"
        MouseGetPos EWD_MouseX, EWD_MouseY
        WinGetPos EWD_WinX, EWD_WinY,,, "ahk_id " EWD_MouseWin
        SetWinDelay -1   <em>; Fenster wird schneller/flüssiger verschoben.</em>
        WinMove EWD_WinX + EWD_MouseX - EWD_MouseStartX, EWD_WinY + EWD_MouseY - EWD_MouseStartY,,, "ahk_id " EWD_MouseWin
        EWD_MouseStartX := EWD_MouseX  <em>; Für den nächsten Timer-Aufruf auf diese Subroutine aktualisieren.</em>
        EWD_MouseStartY := EWD_MouseY
    }
}
</pre>
</body>
</html>
