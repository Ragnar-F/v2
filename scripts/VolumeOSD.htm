<!DOCTYPE HTML>
<html>
<head>
<title>Lautstärke-Bildschirmanzeige (OSD) -- von Rajat</title>
<meta http-equiv="content-type" content="text/html; charset=UTF-8">
<meta http-equiv="X-UA-Compatible" content="IE=edge">
<meta name="robots" content="noindex, nofollow">
<link href="../static/theme.css" rel="stylesheet" type="text/css" />
<script src="../static/content.js" type="text/javascript"></script>
<script src="../static/ga.js" type="text/javascript"></script>
</head>
<body>

<h1>Lautstärke-Bildschirmanzeige (OSD) -- von Rajat</h1>

<p>Dieses Skript ermöglicht beliebige Hotkeys, die Gesamt- und/oder Wave-Lautstärke zu erhöhen oder zu verringern. Beide Lautstärken werden als Balkendiagramme mit unterschiedlichen Farben angezeigt.
</p>
<p><a href="VolumeOSD.ahk">Dieses Skript herunterladen</a>  |  <a href="index.htm">Andere Beispiel-Skripte</a>  |  <a href="../AutoHotkey.htm">Home</a></p>

<pre class="NoIndent"><em>;_________________________________________________
;_______Benutzereinstellungen_____________________________ </em>

<em>; Nur in diesem Bereich oder Hotkey-Bereich Änderungen durchführen!! </em>

<em>; Der Prozentwert, um wieviel die Lautstärke jedes Mal erhöht oder verringert wird:</em>
vol_Step := 4

<em>; Wie lange die Balkendiagramme der Lautstärken angezeigt werden sollen:</em>
vol_DisplayTime := 2000

<em>; Balkenfarbe der Gesamtlautstärke (siehe Hilfe-Datei, um präzisere
; Farbtöne anzugeben):</em>
vol_CBM := "Red"

<em>; Balkenfarbe der Wave-Lautstärke</em>
vol_CBW := "Blue"

<em>; Hintergrundfarbe</em>
vol_CW := "Silver"

<em>; Balkenposition auf dem Bildschirm.  Verwendet "center", um den Balken in dieser Abmessung zu zentrieren:</em>
vol_PosX := "center"
vol_PosY := "center"
vol_Width := 150  <em>; Balkenbreite</em>
vol_Thick := 12   <em>; Balkendicke</em>

<em>; Wenn die aktuelle Tastatur Multimedia-Tasten für die Lautstärke hat, dann
; kannst du versuchen, die unteren Hotkeys so zu ändern, dass sie
; Volume_Up, ^Volume_Up, Volume_Down und ^Volume_Down verwenden:</em>
HotKey, #Up, vol_MasterUp      <em>; Win+Pfeil nach oben</em>
HotKey, #Down, vol_MasterDown
HotKey, +#Up, vol_WaveUp       <em>; Umschalt+Win+Pfeil nach oben</em>
HotKey, +#Down, vol_WaveDown


<em>;___________________________________________ 
;_____automatischer Ausführungsbereich__________________</em>

<em>; HIER DANACH KEINE ÄNDERUNGEN DURCHFÜHREN (es sei denn, du weißt, was du tust).</em>

#SingleInstance

<em>; Progress-Fenster</em>

Gui, +ToolWindow -Caption -Border +Disabled
Gui, Margin, 0, 0
Gui, Add, Progress, vMaster w%vol_Width% h%vol_Thick% c%vol_CBM% background%vol_CW%
Gui, Add, Progress, vWave w%vol_Width% h%vol_Thick% c%vol_CBW% background%vol_CW%
Return

<em>;___________________________________________</em>

vol_WaveUp:
SoundSet, +%vol_Step%, Wave
Gosub, vol_ShowBars
return

vol_WaveDown:
SoundSet, -%vol_Step%, Wave
Gosub, vol_ShowBars
return

vol_MasterUp:
SoundSet, +%vol_Step%
Gosub, vol_ShowBars
return

vol_MasterDown:
SoundSet, -%vol_Step%
Gosub, vol_ShowBars
return

vol_ShowBars:
<em>; Sobald beide Lautstärken vom Benutzer oder von einem externen Programm geändert werden, werden die neuen Lautstärken abgerufen:</em>
SoundGet, vol_Master, Master
SoundGet, vol_Wave, Wave
GuiControl,, Master, %vol_Master%
GuiControl,, Wave, %vol_Wave%
Gui, Show, x%vol_PosX% y%vol_PosY%
SetTimer, vol_BarOff, -%vol_DisplayTime%
return

vol_BarOff:
Gui, Hide
return
</pre>
</body>
</html>
