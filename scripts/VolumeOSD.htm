<!DOCTYPE HTML>
<html lang="de">
<head>
<title>Lautstärke-Bildschirmanzeige - Skriptbeispiel | AutoHotkey v2</title>
<meta name="description" content="Das Lautstärke-Bildschirmanzeige-Skript erlaubt das Drücken von Hotkeys, um die Master-Lautstärke zu erhöhen oder zu verringern." />
<meta http-equiv="content-type" content="text/html; charset=UTF-8">
<meta http-equiv="X-UA-Compatible" content="IE=edge">
<meta name="robots" content="noindex, nofollow">
<link href="../static/theme.css" rel="stylesheet" type="text/css" />
<script src="../static/content.js" type="text/javascript"></script>
<script src="../static/ga.js" type="text/javascript"></script>
</head>
<body>

<h1>Lautstärke-Bildschirmanzeige <span class="headnote">Basierend auf dem v1-Skript von Rajat</span></h1>

<p>Dieses Skript weist Hotkeys Ihrer Wahl zu, um die Master-Lautstärke zu erhöhen und zu verringern.
</p>
<p><a href="VolumeOSD.ahk">Dieses Skript herunterladen</a>  |  <a href="index.htm">Andere Beispiel-Skripte</a>  |  <a href="../AutoHotkey.htm">Home</a></p>

<pre class="NoIndent"><em>; --- Benutzereinstellungen ---</em>

global config := {}

<em>; Der Prozentwert, um wieviel die Lautstärke jedes Mal erhöht oder verringert wird:</em>
config.Step := 4

<em>; Wie lange die Balkendiagramme der Lautstärken angezeigt werden sollen:</em>
config.DisplayTime := 2000

<em>; Balkenfarbe der Master-Lautstärke (siehe Hilfe für präzisere Farbtöne):</em>
config.CBM := "Red"

<em>; Hintergrundfarbe:</em>
config.CW := "Silver"

<em>; Balkenposition auf dem Bildschirm. Mit "center" kann der Balken jeweils zentriert werden:</em>
config.PosX := "center"
config.PosY := "center"
config.Width := 150  <em>; Balkenbreite</em>
config.Thick := 12   <em>; Balkendicke</em>

<em>; Wenn die aktuelle Tastatur Multimedia-Tasten für die Lautstärke hat, dann
; können Sie versuchen, die unteren Hotkeys so zu ändern, dass sie
; Volume_Up und Volume_Down verwenden:</em>
config.MasterUp := "#Up"      <em>; Win+Pfeil nach oben</em>
config.MasterDown := "#Down"

<em>; --- Auto-Ausführungsbereich ---

; HIER DANACH KEINE ÄNDERUNGEN DURCHFÜHREN (es sei denn, Sie wissen, was Sie tun).</em>

#SingleInstance

<em>; Balkenfenster erstellen:</em>
global G := GuiCreate("+ToolWindow -Caption -Border +Disabled")
G.MarginX := 0, G.MarginY := 0
opt := "w" config.Width " h" config.Thick " background" config.CW
G.Add("Progress", opt " vMaster c" config.CBM)

<em>; Hotkeys registrieren:</em>
Hotkey config.MasterUp,   () =&gt; ChangeVolume("+")
Hotkey config.MasterDown, () =&gt; ChangeVolume("-")

<em>; --- Funktionsdefinitionen ---</em>

ChangeVolume(Prefix)
{
    SoundSetVolume(Prefix config.Step)
    G["Master"].Value := Round(SoundGetVolume())
    G.Show("x" config.PosX " y" config.PosY)
    SetTimer "HideWindow", -config.DisplayTime
}

HideWindow()
{
    G.Hide()
}</pre>
</body>
</html>
