<!DOCTYPE HTML>
<html>
<head>
<title>Joystick-Testskript</title>
<meta http-equiv="content-type" content="text/html; charset=UTF-8">
<meta http-equiv="X-UA-Compatible" content="IE=edge">
<meta name="robots" content="noindex, nofollow">
<link href="../static/theme.css" rel="stylesheet" type="text/css" />
<script src="../static/content.js" type="text/javascript"></script>
<script src="../static/ga.js" type="text/javascript"></script>
</head>
<body>

<h1>Joystick-Testskript</h1>

<p>Dieses Skript hilft dabei, die Tastennummern und andere Attribute des aktuellen Joysticks zu bestimmen. Es könnte auch erkennen lassen, ob der aktuelle Joystick kalibriert werden muss; das heißt, ob der Bewegungsbereich jeder Achse von 0 bis 100 Prozent ist. Falls eine Kalibrierung erforderlich ist, verwendet die Systemsteuerung des Betriebssystems oder die mitgelieferte Software des aktuellen Joysticks.
</p>
<p><a href="JoystickTest.ahk">Dieses Skript herunterladen</a>  |  <a href="index.htm">Andere Beispiel-Skripte</a>  |  <a href="../AutoHotkey.htm">Home</a></p>

<pre class="NoIndent"><em>; 16. Juli 2016: Code für Kompatibilität mit AHK v2 überarbeitet
; 6. Juli 2005 : Automatische Erkennung der Joystick-Nummer hinzugefügt.
; 8. Mai 2005  : Fehler behoben: JoyAxes wird nicht länger abgefragt, das heißt,
; ob der Joystick angeschlossen ist.  Einige Joysticks sind
; Gamepads und haben keine einzige Achse.</em>

<em>; Wenn unbedingt der Bedarf besteht, eine bestimmte Joystick-Nummer zu verwenden, ändert
; den folgenden Wert von 0 auf eine Joystick-Nummer (1-16).
; Bei einem Wert von 0 wird die Joystick-Nummer automatisch erkannt:</em>
JoystickNumber := 0

<em>; ENDE DES KONFIGURATIONSBEREICHS. Hier danach keine Änderungen durchführen,
; es sei denn, die allgemeine Funktionalität des Skripts soll geändert werden.</em>

<em>; Joystick-Nummer automatisch erkennen, falls gefordert:</em>
if JoystickNumber &lt;= 0
{
    Loop 16  <em>; Jede Joystick-Nummer auf Existenz überprüfen.</em>
    {
        if GetKeyState(A_Index "JoyName")
        {
            JoystickNumber := A_Index
            break
        }
    }
    if JoystickNumber &lt;= 0
    {
        MsgBox "Das System hat scheinbar keine Joysticks."
        ExitApp
    }
}

#SingleInstance
joy_buttons := GetKeyState(JoystickNumber "JoyButtons")
joy_name := GetKeyState(JoystickNumber "JoyName")
joy_info := GetKeyState(JoystickNumber "JoyInfo")
Loop
{
    buttons_down := ""
    Loop joy_buttons
    {
        if GetKeyState(JoystickNumber "Joy" A_Index)
            buttons_down .= " " A_Index
    }
    axis_info := "X" Round(GetKeyState(JoystickNumber "JoyX"))
    axis_info .= "  Y" Round(GetKeyState(JoystickNumber "JoyY"))
    if InStr(joy_info, "Z")
        axis_info .= "  Z" Round(GetKeyState(JoystickNumber "JoyZ"))
    if InStr(joy_info, "R")
        axis_info .= "  R" Round(GetKeyState(JoystickNumber "JoyR"))
    if InStr(joy_info, "U")
        axis_info .= "  U" Round(GetKeyState(JoystickNumber "JoyU"))
    if InStr(joy_info, "V")
        axis_info .= "  V" Round(GetKeyState(JoystickNumber "JoyV"))
    if InStr(joy_info, "P")
        axis_info .= "  POV" Round(GetKeyState(JoystickNumber "JoyPOV"))
    ToolTip
    (
        joy_name " (#" JoystickNumber "):
        " axis_info "
        Gedrückte Tasten:
        " buttons_down "

        (Zum Beenden Rechtsklick auf Infobereichssymbol)"
    )
    Sleep 100
}
return
</pre>
</body>
</html>
